wp       = _{ SPACE_SEPARATOR | "\t" }
alpha    = _{ 'a'..'z' | 'A'..'Z' }
digit    = _{ '0'..'9' }
alphanum = _{ (alpha | digit)+ }
symbols  = _{ "%20" }
urlchar  = _{ alphanum ~ symbols* }

method  = { "GET" | "POST" | "PUT" | "DELETE" }
version = { "HTTP/1.1" }

scheme      = { "http" | "https" }
host        = { alphanum ~ ("." ~ alphanum)* }
port        = { digit+ }
path        = { ("/" ~ urlchar*)+ }
param       = { param_name ~ "=" ~ param_value }
param_name  = { urlchar }
param_value = { urlchar }

url = { scheme ~ "://" ~ host ~ (":" ~ port)? ~ path? ~ ("?" ~ param ~ ("&" ~ param)*)? }

header_name  = { (alphanum | "-")+ }
header_value = { (LETTER | NUMBER | SYMBOL | "/" | wp)* }
header       = { header_name ~ wp* ~ ":" ~ wp* ~ header_value }
headers      = { header ~ (NEWLINE ~ header)* }

request = { SOI ~ method ~ wp ~ url ~ (wp ~ version)? ~ (NEWLINE ~ headers)? }
